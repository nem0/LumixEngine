import "pipelines/surface_base.inc"

uniform("Material color", "color", {1, 1, 1, 1})
uniform("Roughness", "normalized_float", 1)
uniform("Metallic", "normalized_float", 0)
uniform("Emission", "float", 0)
uniform("Translucency", "normalized_float", 0)

surface_shader [[
	vec4 c = texture(t_albedo, v_uv, -1) * u_material_color;
	data.albedo = c.rgb;
	data.alpha = c.a;
	#ifdef COLOR0_ATTR
		data.albedo.rgb *= v_color.rgb;
	#endif

	#ifdef AO_ATTR
		data.ao = v_ao;
	#else
		data.ao = 1;
	#endif

	#ifdef HAS_AMBIENT_OCCLUSION_TEX
		data.ao *= texture(t_ambient_occlusion, v_uv).r;
	#endif

	#ifdef FUR 
		data.alpha = saturate(data.alpha - v_fur_layer);
	#endif

	
	data.roughness = texture(t_roughness, v_uv).g * u_roughness;
	#ifdef HAS_METALLICMAP
		data.metallic = texture(t_metallic, v_uv).b * u_metallic;
	#else
		data.metallic = u_metallic;
	#endif
	data.N.xz = texture(t_normal, v_uv).xy * 2 - 1;
	data.N.y = sqrt(saturate(1 - dot(data.N.xz, data.N.xz))); 

	#ifdef TANGENT_ATTR
		vec3 tangent = normalize(v_tangent);
		vec3 N = normalize(v_normal);

		mat3 tbn = mat3(
			tangent,
			N,
			normalize(cross(tangent, v_normal))
		);
		data.N = tbn * data.N;
	#else
		data.N = normalize(v_normal);
	#endif
	data.emission = u_emission;
	data.translucency = u_translucency;
	data.shadow = 1;

	#ifndef ALPHA_CUTOUT
		float ndotv = abs(dot(data.N , data.V)) + 1e-5f;
		data.alpha = mix(data.alpha, 1, pow(saturate(1 - ndotv), 5));
	#endif
]]